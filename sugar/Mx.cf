Deriv.  Prog ::= [Alias] [TyVar] [Binder] "|-" Seq ;

separator Binder "," ;
separator nonempty Id ","  ;
separator AliasId "" ;
terminator TyVar "," ;
terminator Alias ";" ;

TyAlias. Alias   ::= "type" Id [AliasId] "=" Type ;
AliasId. AliasId ::= Id ;

TyVar.   TyVar   ::= Id;

Binder.  Binder  ::= Id ":" Type ;

-- Maybe binder
BJust.    MBinder ::= Id ":" Type ;
BNothing. MBinder ::= Id ;

-- Crashing along
AJust.    Along   ::= "along" [Id] ;
ANothing. Along   ::= ;

-- Multiplicatives
Tensor.   Type4 ::= Type4 "*" Type5 ;
Par.      Type4 ::= Type4 "|" Type5 ;
One.      Type5 ::= "1"   ;
Bot.      Type5 ::= "_|_" ;

-- Additives
Plus.     Type3 ::= Type3 "+" Type4 ;
Choice.   Type3 ::= Type3 "&" Type4 ;
Top.      Type5 ::= "T" ;
Zero.     Type5 ::= "0" ;

Lollipop. Type1 ::= Type2 "-o" Type1 ;

TyId.     Type5 ::= Id ;

Bang.     Type5 ::= "!" Type5 ;
Quest.    Type5 ::= "?" Type5 ;
Neg.      Type5 ::= "~" Type5 ;

-- TODO: Support these applied to (partially-applied) type aliases
Forall.   Type ::= "forall" Id "." Type ;
Exists.   Type ::= "exists" Id "." Type ;
Mu.       Type ::= "mu" Id "." Type ;

coercions Type 5 ;

-- I don't like calling these fst and snd because these are about sums, not tuples
Fst.       Choice ::= "fst" ;
Snd.       Choice ::= "snd" ;

Ax.        Seq ::= Id "<->" Id;
Cut.       Seq ::= "connect" "{" MBinder "->" Seq ";" MBinder "->" Seq "}";
ParSeq.    Seq ::= "connect" "via" Id "{" Id "->" Seq ";" Id "->" Seq "}";
TensorSeq. Seq ::= "let" Id "," Id "=" Id "in" Seq;
ChoiceSeq. Seq ::= "let" Id "=" Choice Id "in" Seq;
Case.      Seq ::= "case" Id "of" "{" "inl" Id "->" Seq ";" "inr" Id "->" Seq "}" ;
Bottom.    Seq ::= Id ;
Unit.      Seq ::= "let" "()" "=" Id "in" Seq ;
Crash.     Seq ::= "crash" Id Along ;
Pack.      Seq ::= "let" Id "=" Id "@" Type "in" Seq ;
Unpack.    Seq ::= "let" Id "@" Id "=" Id "in" Seq ;
Offer.     Seq ::= "offer" Id "for" Id "in" Seq ;
Demand.    Seq ::= "let" Id "=" "demand" Id "in" Seq ;
Ignore.    Seq ::= "ignore" Id "in" Seq ;
Alias.     Seq ::= "let" Id "=" "alias" Id "in" Seq ;
Fold.      Seq ::= "let" Id "=" "fold" Id "in" Seq ;
Unfold.    Seq ::= "let" Id "=" "unfold" Id "in" Seq ;
Hole.      Seq ::= "_" ;

position token Id (letter)((letter|digit|'-'|'_'|'\'')*) ;

comment "{-" "-}" ;
comment "--" ;

